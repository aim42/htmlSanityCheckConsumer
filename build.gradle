
buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // in case of mavenLocal(), the following line is valid:
        classpath(group: 'org.aim42',

                // in case of using the official Gradle plugin repository:
                //classpath (group: 'gradle.plugin.org.aim42',
                name: 'htmlSanityCheck',
                version: '1.0.0-RC-2')

    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.8'
}

project.version = "0.3.2"


// ==== path definitions =====
// ===========================
ext {
    srcDir = "$projectDir/src/docs/asciidoc"

// location of images used in AsciiDoc documentation
    srcImagesPath = "$srcDir/images"

// results of asciidoc compilation (HTML)
// (input for htmlSanityCheck)
// this is the default path for asciidoc-gradle-convert
    htmlOutputPath = "$buildDir"

// images used by generated html
    targetImagesPath = "$buildDir/images"

}

// ==== asciidoctor ==========
// ===========================

apply plugin: 'org.asciidoctor.convert'

asciidoctor {
    outputDir = file(buildDir)
    sourceDir = file(srcDir)

    sources {
        include "many-errors.adoc", "no-errors.adoc"
    }

    attributes = [
            doctype    : 'book',
            icons      : 'font',
            sectlink   : true,
            sectanchors: true]

    resources {
        from(srcImagesPath) { include '**' }
        into "./images"
    }


}

// ========================================================
apply plugin: 'org.aim42.htmlSanityCheck'


htmlSanityCheck {

    // ensure asciidoctor->html runs first
    // and images are copied to build directory

    dependsOn asciidoctor

    sourceDir = new File("${buildDir}/html5")

    // files to check, in Set-notation
    sourceDocuments = ["many-errors.html", "no-errors.html"]

    // fail the build if any error is encountered
    failOnErrors = false

    // set the http connection timeout to 2 secs
    httpConnectionTimeout = 2000

    ignoreLocalHost = false
    ignoreIPAddresses = false


}

// when called without arguments, gradle will execute the default:
defaultTasks 'htmlSanityCheck'


task info(
        description: "info on several paths"
) { doLast
        {
            println "=" * 100
            println "project.name         : " + project.name
            println "projectDir           : " + projectDir
            println "buildDir             : " + buildDir
            println "source images        : " + srcImagesPath
            println "asciidoc html output : " + htmlOutputPath
            println "target images path   : " + targetImagesPath
        }
}
